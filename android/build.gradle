plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.dokka'
    id 'kover'
    id 'maven-publish'
}

group = 'com.veryfi'
version = '1.0.3'

afterEvaluate{
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.veryfi'
                artifactId = 'veryfi-android'
                version = '1.0.3'
                pom {
                    name = 'Veryfi Android'
                    description = 'Android kotlin module for communicating with the Veryfi OCR API'
                    url = 'https://github.com/veryfi/veryfi-android'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'androide0917'
                            name = 'Yuber Garcia'
                            email = 'yubergarcia@veryfi.com'
                        }
                        developer {
                            id = 'kaevan89'
                            name = 'Sebastian Carmona'
                            email = 'sebastiancarmona@veryfi.com'
                        }
                        developer {
                            id = 'alejouribesanchez'
                            name = 'Alejandro Uribe'
                            email = 'alejandro@veryfi.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/veryfi/veryfi-android.git'
                        developerConnection = 'scm:git:ssh://github.com/veryfi/veryfi-android.git'
                        url = 'https://github.com/veryfi/veryfi-android/'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/releases'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
    }
}

android {
    compileSdk 31
    namespace 'com.veryfi.android'
    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 2
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            if (name == "testDebugUnitTest") {
                kover {
                    enabled = true
                    binaryReportFile.set(file("$buildDir/custom/debug-report.bin"))
                    includes = ['veryfi\\..*']
                }
            }
        }
    }
}

tasks.koverHtmlReport {
    enabled = true                          // false to disable report generation
    htmlReportDir.set(layout.buildDirectory.dir("my-reports/html-result"))
}

tasks.koverXmlReport {
    enabled = true
    xmlReportFile.set(layout.buildDirectory.file("my-reports/result.xml"))
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'

    //Unit Tests
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation 'org.json:json:20180813'

    //Kover
    implementation "androidx.core:core-ktx:1.8.0"

    //RxAndroid for async calls
    implementation 'io.reactivex.rxjava2:rxjava:2.2.4'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
}